# ---------------------------- GUI Function ---------------------------------

#스킨 메인
function Skin_Purchase_main(p: player):
	set {_gui} to chest inventory with 1 rows named "스킨 아이템 구매"

	set slot 1 of {_gui} to netherite pickaxe named "곡괭이" with lore "곡괭이 스킨을 구매합니다"
	set slot 3 of {_gui} to netherite axe named "도끼" with lore "도끼 스킨을 구매합니다"
	set slot 5 of {_gui} to netherite sword named "검" with lore "검 스킨을 구매합니다"
	set slot 7 of {_gui} to netherite chestplate named "갑옷" with lore "갑옷 스킨을 구매합니다"
	open {_gui} to {_p}

#검 스킨 구매
function Sword_Skin_Purchase(p: player):
	set {_gui} to chest inventory with 1 rows named "검 스킨 구매"

	set slot 0 of {_gui} to netherite sword named "TEST Sword" with lore "테스트 검 스킨" and "가격 : 5000DP" and "내구도 : 10155" and "[추가 옵션 목록]" and "&6➤ DLY (50%%)" and "&6➤ ATK (50%%)" with custom model data 1
	open {_gui} to {_p}

# ---------------------------- Attribute Function ---------------------------------

#모든 Attribute 함수
#Attribute Funciton (Attack Damage) - 적용 : 검, 도끼
#item = 적용할 아이템
#min/maxamount = 최소/최대값
#baseamount = 기본값
#예제 : set {_item} to AttackDamageModifier({_item}, 2, 5, 10) => {_item}에 Attack Damage를 2 ~ 5를 추가한다. 해당 아이템의 기본 값은 10
	
function AttackDamageModifier(item: item, minamount: integer, maxamount: integer, baseamount: integer) :: item:
	set {_randomAmount} to random integer between {_minamount} and {_maxamount}
	add "[추가 옵션] &6➤ &cATK &7+ &a%{_randomAmount}%" to lore of {_item}
	set {_finalAmount} to {_randomAmount} + {_baseamount} - 1
	apply attribute modifier to {_item}:
		attribute: attack damage
		id: "attackdamage"
		slot: mainhand_slot_group
		amount: {_finalAmount}
		operation: add_number
	return {_item}

#Attribute Funciton (Attack Speed) - 적용 : 검, 도끼
#item = 적용할 아이템
#min/maxamount = 최소/최대값
#예제 : set {_item} to AttackSpeedModifier(({_item}, 2, 5) =>{_item}에 Attack Speed를 2% ~ 5% 추가한다.

function AttackSpeedModifier(item: item, minamount: integer, maxamount: integer) :: item:
	set {_randomAmount} to random integer between {_minamount} and {_maxamount}
	add "[추가 옵션] &6➤ &cDLY &7- &a%{_randomAmount}%%%" to lore of {_item}
	set {_scaledAmount} to {_randomAmount} / 100
	apply attribute modifier to {_item}:
		attribute: attack speed
		id: "attackspeed"
		slot: mainhand_slot_group
		amount: {_scaledAmount}
		operation: add_scalar
	return {_item}

#Attribute Funciton (MaxHealth) - 적용 : 갑옷, 방패
#item = 적용할 아이템
#min/maxamount = 최소/최대값
#예제 : set {_item} to MaxHealthAttributeModifier({_item}, 2, 5, "head_slot_group") => {_item}을 "head_slot_group"에 착용했을 때 maxHealth를 2 ~ 5 추가한다.
function MaxHealthAttributeModifier(item: item, minamount: integer, maxamount: integer, slotGroup: string) :: item:
	set {_randomAmount} to random integer between {_minamount} and {_maxamount}
	add "[추가 옵션] &6➤ &cMaxHealth &7+ &a%{_randomAmount}%" to lore of {_item}
	set {_scaledAmount} to {_randomAmount}
	apply attribute modifier to {_item}:
		attribute: max health
		id: "maxhealth"
		slot: {_slotGroup}
		amount: {_scaledAmount}
		operation: add_number
	return {_item}

#Attribute Funciton (ArmorToughness) - 적용 : 갑옷
#item = 적용할 아이템
#min/maxamount = 최소/최대값
#예제 : set {_item} to ArmorToughnessAttributeModifier({_item}, 2, 5, "head_slot_group") => {_item}을 "head_slot_group"에 착용했을 때 ArmorToughness를 2 ~ 5 추가한다.
function ArmorToughnessAttributeModifier(item: item, minamount: integer, maxamount: integer, slotGroup: string) :: item:
	set {_randomAmount} to random integer between {_minamount} and {_maxamount}
	add "[추가 옵션] &6➤ &cAT &7+ &a%{_randomAmount}%" to lore of {_item}
	apply attribute modifier to {_item}:
		attribute: armor toughness
		id: "armortoughness"
		slot: {_slotGroup}
		amount: {_randomAmount}
		operation: add_number
	return {_item}

#Attribute Funciton (Movement Speed) - 적용 : 갑옷
#item = 적용할 아이템
#min/maxamount = 최소/최대값
#예제 : set {_item} to MovementSpeedAttributeModifier({_item}, 2, 5, "head_slot_group") => {_item}을 "head_slot_group"에 착용했을 때 movement speed를 2 ~ 5% 추가한다.
function MovementSpeedAttributeModifier(item: item, minamount: integer, maxamount: integer, slotGroup: string) :: item:
	set {_randomAmount} to random integer between {_minamount} and {_maxamount}
	add "[추가 옵션] &6➤ &cSPD &7-&a%{_randomAmount}%%%" to lore of {_item}
	set {_scaledAmount} to {_randomAmount} / 100
	apply attribute modifier to {_item}:
		attribute: movement speed
		id: "movementspeed"
		slot: {_slotGroup}
		amount: {_scaledAmount}
		operation: add_scalar
	return {_item}

# ---------------------------- Inventory Click Section ---------------------------------

#스킨 메인 GUI 
on inventory click:
	name of event-inventory is "스킨 아이템 구매"
	cancel event
	event-inventory is not player's inventory
	if clicked slot is 1:
		close player's inventory
		send "준비중"
	else if clicked slot is 3:
		close player's inventory
		send "준비중"
	else if clicked slot is 5:
		close player's inventory
		Sword_Skin_Purchase(player)
	else if clicked slot is 7:
		close player's inventory
		send "준비중"
	

#검 스킨 페이지
on inventory click:
	name of event-inventory is "검 스킨 구매"
	cancel event
	event-inventory is not player's inventory
	if clicked slot is 0:
		if {DP::%player's uuid%} >= 5000:
			#DP Remove
			subtract 5000 from {DP::%player's uuid%} 
			#Default Setting
			set {_item} to netherite sword named "테스트스킨무기" with lore "This Is TEST Weapon" with nbt from "{""max_damage"":10155,""damage"":1,""repair_cost"":999,""custom_model_data"":1}" 
			#add Line break to item lore
			add " " to lore of {_item}
			#Apply Attribute Modifier (With Chance)
			chance of 50%:
				set {_item} to AttackDamageModifier({_item}, 1, 5, 8) #네더라이트 검의 기본 데미지는 8
			chance of 50%:
				set {_item} to AttackSpeedModifier({_item}, 1, 5)
			#Hide Attribute
			add hide attributes to item flags of {_item} 
			#give item to player
			give player 1 of {_item}
		else:
			send "기부 포인트가 부족합니다." to player
			
		close player's inventory
